plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "signing"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "overwolfgameevents"
version = "1.0.0"

configurations {
    provided {
        compile.extendsFrom(provided)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api("net.java.dev.jna:jna:5.6.0")
    api("org.checkerframework:checker-qual:3.10.0")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    failOnError = false
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-${archivesBaseName}")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
            pom {
                name = "OverwolfGameEvents"
                description = "Unofficial Overwolf Game Events SDK Bindings for Java"
                url = "https://github.com/LXGaming/OverwolfGameEvents"
                developers {
                    developer {
                        id = "lxgaming"
                        name = "LXGaming"
                    }
                }
                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/LXGaming/OverwolfGameEvents/issues"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "scm:git@github.com:LXGaming/OverwolfGameEvents.git"
                    developerConnection = "scm:git@github.com:LXGaming/OverwolfGameEvents.git"
                    url = "https://github.com/LXGaming/OverwolfGameEvents"
                }
            }
        }
    }
    repositories {
        if (project.hasProperty("sonatype.username") && project.hasProperty("sonatype.password")) {
            maven {
                name = "sonatype"
                url = version.contains("-SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username project.property("sonatype.username")
                    password project.property("sonatype.password")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}